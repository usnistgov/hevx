#version 460 core
#extension GL_NV_ray_tracing : require
#extension GL_GOOGLE_include_directive : require

layout(push_constant) uniform PushConstants {
  vec4 padding0;
  float iTime;
  float iTimeDelta;
  float iFrameRate;
  float iFrame;
  vec3 padding1;
  bool bDebugNormals;
  vec4 EyePosition;
  mat4 ModelMatrix;
  mat4 ModelViewMatrix;
  mat3 NormalMatrix;
};

struct Sphere {
  float aabbMinX;
  float aabbMinY;
  float aabbMinZ;
  float aabbMaxX;
  float aabbMaxY;
  float aabbMaxZ;
};

layout(std430, set = 1, binding = 2) readonly buffer SphereBuffer {
  Sphere spheres[];
};

hitAttributeNV vec3 Po; // Hit position in world-space
hitAttributeNV vec3 No; // Normal in world-space

void main() {
  /////
  //
  // All of these coordinates are in world space.
  //
  ////

  const vec3 origin = gl_WorldRayOriginNV;
  const vec3 direction = normalize(gl_WorldRayDirectionNV);

  Sphere sphere = spheres[gl_PrimitiveID];
  const vec3 aabbMin = vec3(sphere.aabbMinX, sphere.aabbMinY, sphere.aabbMinZ);
  const vec3 aabbMax = vec3(sphere.aabbMaxX, sphere.aabbMaxY, sphere.aabbMaxZ);

  const vec3 center =
    (ModelMatrix * vec4((aabbMax.xyz + aabbMin.xyz) / vec3(2.f), 1.f)).xyz;
  const float radius = (aabbMax.x - aabbMin.x) / 2.f;

  const vec3 oc = origin - center;
  const float a = dot(direction, direction);
  const float b = dot(oc, direction);
  const float c = dot(oc, oc) - (radius * radius);
  const float d = b * b - a * c;

  if (d > 0.f) {
    const float s_d = sqrt(d);
    const float t1 = (-b - s_d) / a;
    const float t2 = (-b + s_d) / a;

    if (gl_RayTminNV < t1 && t1 < gl_RayTmaxNV) {
      Po = origin + direction * t1;
      No = (Po - center) / radius;
      reportIntersectionNV(t1, 0);
    } else if (gl_RayTminNV < t2 && t2 < gl_RayTmaxNV) {
      Po = origin + direction * t2;
      No = (Po - center) / radius;
      reportIntersectionNV(t2, 0);
    }
  }
}

